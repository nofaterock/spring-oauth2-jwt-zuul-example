plugins {
	id 'org.springframework.boot' version '2.2.2.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
}

apply plugin: "idea"

group = 'com.nofaterock.oauth2'
version = '0.0.1-SNAPSHOT'

idea {
	module {
		excludeDirs = [file(".idea"), file(".gradle"), file(".file")]
	}
}

ext {
	set('springCloudVersion', "Hoxton.SR1")
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	sourceCompatibility = '1.8'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		// lombok
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		// test
		testImplementation('org.springframework.boot:spring-boot-starter-test') {
			exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		}
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	test {
		useJUnitPlatform()
	}

	idea {
		module {
			outputDir = file("${buildDir}/classes/main")
			testOutputDir = file("${buildDir}/classes/test")
		}
	}
}
